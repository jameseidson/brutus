load("@crate_index//:defs.bzl", "all_crate_deps")
load("@rules_rust//cargo:defs.bzl", "cargo_build_script")
load("@rules_rust//rust:defs.bzl", "rust_static_library")

rust_static_library(
    name = "server",
    srcs = glob(["src/**/*.rs"]),
    rustc_env = {
        "BAZEL_PROTO_COMMAND_MODULE": "$(execpath :proto)/command_capnp.rs",
        "BAZEL_PROTO_EVENT_MODULE": "$(execpath :proto)/event_capnp.rs",
    },
    visibility = ["//visibility:public"],
    deps = all_crate_deps() + [":proto"],
)

cargo_build_script(
    name = "proto",
    srcs = ["build.rs"],
    build_script_env = {
        "BAZEL_CAPNP_COMPILER_PATH": "$(execpath @capnproto-cpp-1.0.2//:compiler)",
        "BAZEL_PROTO_COMMAND_SRC": "$(execpath //src/common/proto:command)",
        "BAZEL_PROTO_EVENT_SRC": "$(execpath //src/common/proto:event)",
        "BAZEL_PROTO_GO_INCLUDE_PATH": "$(execpath @org_capnproto_go_capnp_v3//:std)",
    } | select({
        "//src/common/proto:use-system-capnp": {"BAZEL_CAPNP_COMPILER_PATH": "capnp"},
        "//conditions:default": {"BAZEL_CAPNP_COMPILER_PATH": "$(execpath @capnproto-cpp-1.0.2//:compiler)"},
    }),
    data = [
        "//src/common/proto:event",
        "//src/common/proto:command",
        "@org_capnproto_go_capnp_v3//:std",
    ] + select({
        "//src/common/proto:use-system-capnp": [],
        "//conditions:default": ["@capnproto-cpp-1.0.2//:compiler"],
    }),
    visibility = ["//visibility:private"],
    deps = ["@crate_index__capnpc-0.19.0//:capnpc"],
)
